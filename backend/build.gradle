plugins {
    id 'java'
    id 'application'
    id 'nu.studer.jooq' version '3.0.3'
}

group 'me.mtte.code'
version '0.0.1'
mainClassName = 'me.mtte.code.ideahub.Application'

sourceCompatibility = 11

repositories {
    mavenCentral()
}

dependencies {
    // Spark
    implementation group: 'com.sparkjava', name: 'spark-core', version: '2.8.0'

    // DB
    implementation group: 'com.zaxxer', name: 'HikariCP', version: '3.4.1'
    implementation group: 'org.jooq', name: 'jooq'
    runtime group: 'org.postgresql', name: 'postgresql', version: '42.2.8'
    jooqRuntime group: 'org.jooq.trial', name: 'jooq-meta-extensions', version: '3.12.3'

    // Logging
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.7'
    runtime 'org.codehaus.groovy:groovy-all:2.4.3'

    // Tests
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jooq {
    version = '3.12.3'
    edition = 'OSS'
    database(sourceSets.main) {
        generator {
            database {
                name = 'org.jooq.meta.extensions.ddl.DDLDatabase'
                properties {
                    property {
                        key = 'scripts'
                        value = '../database/01_setup.sql'
                    }
                    property {
                        key = 'defaultNameCase'
                        value = 'lower'
                    }
                }
            }
            target {
                packageName = 'me.mtte.code.ideahub.database'
                directory = 'src/main/java'
                clean = true
            }
        }
    }
}